<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
       "http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
<title>Mars Planet Webshop</title>
<meta http-equiv="content-type" content="text/html;charset=utf-8" />
<meta http-equiv="Content-Style-Type" content="text/css" />
</head>
<body>
<?cob 
*>  start or resume session
    PERFORM HTM2COB-SESS-START
    DISPLAY "<br><pre>HTM2COB-HIDDEN-SESSION-ID: " HTM2COB-HIDDEN-SESSION-ID"</pre>"
?>

<h2>Mars Planet Webshop</h2>
<h5>Sorry, there is no return ticket back to Earth...</h5>

<h3>Checkout</h3>
<p>
<?cob PERFORM READ-CART-ITEMS-FROM-SESS ?>
<b>Total Price ($): <?cob DISPLAY WS-TOTAL-PRICE-DISP ?></b><br><br>
Please transfer the total amount to the given bank account.
<b>IBAN: ...</b> 
</p>

<p> 
<a href="http://localhost/0035_ck_sess_regen_cart_test.html">Mars Planet Webshop start page</a>
</p>

</body>
</html>

<?cob-ws 
 COPY "shopdata.cpy".

 01 WS-IND                       PIC 9(4). 
 01 WS-TOTAL-PRICE-DISP          PIC ZZZ,ZZZ,ZZZ,ZZ9.99.

 01 WS-SEL-CAT-ITEM.
  02 FILLER                      PIC X(3) VALUE "cat".
  02 WS-SEL-CAT-NR               PIC 9(1).
  02 FILLER                      PIC X(5) VALUE "_item".
  02 WS-SEL-ITEM-NR              PIC 9(1).
 01 WS-QTY                       PIC 9(4). 
 01 WS-SUBTOTAL-PRICE            PIC 9(9)V9(2). 
 01 WS-TOTAL-PRICE               PIC 9(12)V9(2). 
?>

<?cob
*>------------------------------------------------------------------------------
 READ-CART-ITEMS-FROM-SESS SECTION.
*>------------------------------------------------------------------------------
     
    INITIALIZE WS-TOTAL-PRICE
    
    PERFORM VARYING WS-IND FROM 1 BY 1
            UNTIL WS-IND > CAT-ITEM-MAX-IND
       MOVE WS-CAT-NR      OF WS-CAT-ITEM-TAB(WS-IND) TO WS-SEL-CAT-NR     
       MOVE WS-CAT-ITEM-NR OF WS-CAT-ITEM-TAB(WS-IND) TO WS-SEL-ITEM-NR     

*>     read from session
       MOVE WS-SEL-CAT-ITEM TO HTM2COB-SESS-VAR-NAME
       PERFORM HTM2COB-SESS-GET

       IF V-OK OF LNK-RESULT OF LNK-HTM2COB-SESSION
       THEN
          MOVE HTM2COB-SESS-VAR-VALUE TO WS-QTY
        
          IF  FUNCTION TEST-NUMVAL(WS-QTY) = ZEROES
          AND FUNCTION NUMVAL(WS-QTY) > ZEROES
          THEN
             COMPUTE WS-SUBTOTAL-PRICE = WS-CAT-ITEM-PRICE OF WS-CAT-ITEM-TAB(WS-IND)
                                       * FUNCTION NUMVAL(WS-QTY)
             END-COMPUTE
   
             COMPUTE WS-TOTAL-PRICE = WS-TOTAL-PRICE + WS-SUBTOTAL-PRICE
             END-COMPUTE
             MOVE WS-TOTAL-PRICE TO WS-TOTAL-PRICE-DISP          
          END-IF
       END-IF   
    END-PERFORM

    EXIT SECTION .
    
*> Implement this section, if you want to set a cookie.
*>------------------------------------------------------------------------------
 HTM2COB-SET-COOKIE SECTION.
*>------------------------------------------------------------------------------

*>  get remote address and http user agent of the visitor
    PERFORM HTM2COB-GET-SESS-ENV-PARAMS

*>  get session-id from cookie
    INITIALIZE LNK-HTM2COB-GET-COOKIE
    MOVE "SESSIONID" TO LNK-COOKIE-NAME OF LNK-HTM2COB-GET-COOKIE
    CALL "HTM2COB-GET-COOKIE" USING LNK-HTM2COB-GET-COOKIE END-CALL
    MOVE FUNCTION TRIM(LNK-COOKIE-VALUE OF LNK-HTM2COB-GET-COOKIE)
      TO HTM2COB-HIDDEN-SESSION-ID

*>  regenerate session
    PERFORM HTM2COB-SESS-REGENERATE
*>  update session-id in cookie string
    MOVE HTM2COB-HIDDEN-SESSION-ID 
      TO LNK-SESSION-ID-HEX OF LNK-HTM2COB-UPD-SESS-STR
    CALL "HTM2COB-UPD-SESS-STR" USING LNK-HTM2COB-UPD-SESS-STR END-CALL

*>  save session-id in cookie
    INITIALIZE LNK-HTM2COB-SET-COOKIE
    SET V-SET OF LNK-COOKIE-FUNC OF LNK-HTM2COB-SET-COOKIE TO TRUE
    MOVE "SESSIONID"               TO LNK-COOKIE-NAME  OF LNK-HTM2COB-SET-COOKIE
    MOVE HTM2COB-HIDDEN-SESSION-ID TO LNK-COOKIE-VALUE OF LNK-HTM2COB-SET-COOKIE
    MOVE 1800                      TO LNK-MAX-AGE-SEC  OF LNK-HTM2COB-SET-COOKIE
    MOVE "localhost"               TO LNK-DOMAIN-VALUE OF LNK-HTM2COB-SET-COOKIE
    MOVE "/"                       TO LNK-PATH-VALUE   OF LNK-HTM2COB-SET-COOKIE
    SET V-YES OF LNK-HTTPONLY-FLAG OF LNK-HTM2COB-SET-COOKIE TO TRUE
    SET V-YES-LAX OF LNK-SAMESITE-FLAG OF LNK-HTM2COB-SET-COOKIE TO TRUE
    CALL "HTM2COB-SET-COOKIE" USING LNK-HTM2COB-SET-COOKIE END-CALL
    *> The last DISPLAY in this section is always with "WITH NO ADVANCING"
    DISPLAY FUNCTION TRIM(LNK-COOKIE-STR OF LNK-HTM2COB-SET-COOKIE) WITH NO ADVANCING END-DISPLAY

    EXIT SECTION .
?>


